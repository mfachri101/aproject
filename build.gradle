buildscript {
  dependencies {
    classpath("org.liquibase:liquibase-core:4.23.2")
  }
}

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.9'
	id 'io.spring.dependency-management' version '1.1.7'
  id 'org.liquibase.gradle' version '3.0.2'
  id 'nu.studer.jooq' version "10.2"
}
group = 'com.fachri'
version = '0.0.1-SNAPSHOT'
description = 'A Project'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-jooq'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.liquibase:liquibase-core'
  implementation 'org.jooq:jooq'

  // Swagger / OpenAPI
  implementation 'io.swagger.core.v3:swagger-annotations:2.2.41'
  implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.15'

  runtimeOnly 'org.postgresql:postgresql'

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.boot:spring-boot-testcontainers'
  testImplementation 'org.testcontainers:junit-jupiter'
  testImplementation 'org.testcontainers:postgresql'

  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

  liquibaseRuntime sourceSets.main.output
  liquibaseRuntime 'info.picocli:picocli:4.7.7'
  liquibaseRuntime 'org.liquibase:liquibase-core'
  liquibaseRuntime 'org.postgresql:postgresql'

  jooqGenerator 'org.postgresql:postgresql'
  jooqGenerator 'org.jooq:jooq-codegen'
  jooqGenerator 'org.jooq:jooq-meta'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.named('compileJava') {
  dependsOn(tasks.named('generateJooq'))
}

liquibase {
  activities {
    main {
      changelogFile 'src/main/resources/db/changelog.yaml'

      arguments = [
        changelogFile: 'src/main/resources/db/changelog.yaml',
        url: 'jdbc:postgresql://localhost:5432/loan_database',
        username: 'mycustomuser',
        password: 'mysecretpassword',
        driver: 'org.postgresql.Driver',
        defaultSchemaName: 'loan_schema',
        liquibaseSchemaName: 'loan_schema'
      ]
    }
  }
}

jooq {
  version = '3.19.29'
  configurations {
    main {
      generationTool {
        logging = 'WARN'
        jdbc {
          driver = 'org.postgresql.Driver'
          url = 'jdbc:postgresql://localhost:5432/loan_database'
          user = 'mycustomuser'
          password = 'mysecretpassword'
        }
        generator {
          name = 'org.jooq.codegen.DefaultGenerator'
          database {
            name = 'org.jooq.meta.postgres.PostgresDatabase'
            inputSchema = 'loan_schema'
            // Optional: only include specific tables
            includes = '.*'
            excludes = 'databasechangelog|databasechangeloglock'
          }
          target {
            packageName = 'com.fachri.aproject.generated'
            directory = 'build/generated-sources/jooq'
          }
          strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
        }
      }
    }
  }
}